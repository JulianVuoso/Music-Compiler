<start>           ::=  "start" "new_line" <program> "stop" "new_line"                        

<program>         ::=  <program> <declare>                                                  
                    |  <program> <assign>                                                 
                    |  <program> <if_sentence>                                            
                    |  <program> <do_sentence>                                            
                    |  <program> <while_sentence>                                         
                    |  <program> <play>                                                   
                    |  <program> <print>                                                  
                    |  <program> <getter>                                                 
                    |  <program> "new_line"                                                 
                    |                                                         
                    ;

<play>            ::= "play" "(" <expression> ")" "new_line"                   
                    ;

<print>           ::= "print" "(" <expression> ")" "new_line"                  
                    | "print" "(" "string" ")" "new_line"                      
                    ;

<getter>          ::= "getnum" "(" "variable" ")" "new_line"                       
                    | "getchord" "(" "variable" ")" "new_line"                     
                    ;

<do_sentence>     ::= "do" <body> "while" <compare> "new_line"                                    
                    ;

<while_sentence>  ::= "while" <compare> <body> "new_line"                                       
                    ;

<if_sentence>     ::= "if" <compare> <body> "new_line"                                          
                    | "if" <compare> <body> "else" <body> "new_line"                                
                    ;

<compare>         ::= "(" <mult_compare> <any_op> <single_compare> ")"         
                    | "(" <single_compare> ")"                             
                    ;

<single_compare>  ::= "(" <mult_compare> <any_op> <single_compare> ")"         
                    | "!" "(" <mult_compare> ")"                        
                    | <expression>                                                       
                    ;

<mult_compare>    ::= <mult_compare> <any_op> <single_compare>                                                                
                    | <single_compare>                                                    
                    ;

<any_op>          ::= <add_op_logic>                         
                    | <op_compare>                           
                    ;
                
<body>            ::= "{" <program> "}"     
                    ;

<add_op_logic>    ::= "and"                               
                    | "or"                                
                    ;

<op_compare>      ::= ">"                                
                    | ">="                                   
                    | "<"                                
                    | "<="                               
                    | "=="                             
                    | "!="                         
                    ;

<declare>         ::= <var_type> "variable" "new_line"                      
                    ;

<assign>          ::= "variable" "=" <expression> "new_line"        
                    ;

<var_type>        ::= "int"                                                          
                    | "chord"                            
                    | "set"                             
                    ;

<expression>      ::= <expression> "+" <term>                   
                    | <expression> "-" <term>                 
                    | <term>                                  
                    ;

<term>            ::= <term> "*" <factor>                       
                    | <term> "/" <factor>                       
                    | <factor>                                  
                    ;

<factor>          ::= <constant>                                            
                    | "variable"                                                   
                    | "[" <expression> <expression> "]"     
                    | "(" <expression> ")"                     
                    ;

<constant>        ::= "chor"                                 
                    | "number"                               
                    | "note"                              
                    ;
